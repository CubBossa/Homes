plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.1'
}

group = 'de.pqtriick'
version = '1.5.1'

repositories {
    mavenCentral()
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        url = 'https://nexus.leonardbausenwein.de/repository/maven-public/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.20-R0.1-SNAPSHOT'
    compileOnly 'com.github.azbh111:craftbukkit-1.8.8:R'
    implementation 'de.cubbossa:TinyTranslations:4.1.2'
    compileOnly "net.kyori:adventure-api:4.14.0"
    compileOnly "net.kyori:adventure-platform-bukkit:4.3.1"
    compileOnly "net.kyori:adventure-text-minimessage:4.14.0"
}

def targetJavaVersion = 8
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}


tasks.build {
    dependsOn("shadowJar")
}

tasks.shadowJar {
    configurations = [project.configurations.runtimeClasspath]
    relocate "de.cubbossa.tinytranslations", "de.pqtriick.homes.libs.tinytranslations"
    minimize()
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}
